buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

ext.configFile = file 'build.properties'

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod_version}.${System.getenv("BUILD_NUMBER") ?: System.getenv("CIRCLE_BUILD_NUM") ?: 0}-${config.release_type}"
group= "at.korti.transmatrics"
archivesBaseName = "Transmatrics"

sourceCompatibility = config.java_version
targetCompatibility = config.java_version

repositories{
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url "http://mobiusstrip.eu/maven"
    }
    ivy{
        name "BuildCraft"
        artifactPattern "http://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision](-[classifier]).[ext]"
    }
}

minecraft {
    version = "1.10-12.18.0.2000-1.10.0"
    runDir = "run"
    mappings = "snapshot_20160531"
    useDepAts = true
    makeObfSourceJar = false

    replace "@VERSION@", project.version
    replaceIn "Constants.java"
}

dependencies {
    deobfCompile "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}"
    deobfCompile "slimeknights.mantle:Mantle:${config.minecraft_version}-${config.mantel_version}"
    deobfCompile "slimeknights:TConstruct:${config.minecraft_version}-${config.tic_version}"
    provided  name: "buildcraft", version: config.buildcraft_version, classifier: "api"

    compile "mcp.mobius.waila:Waila:${config.waila_version}"
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

version = "${config.minecraft_version}-${version}"

task deobfJar(type: Jar){
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts{
    archives deobfJar
}